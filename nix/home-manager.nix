{
  config,
  lib,
  pkgs,
  ...
}:
let
  cfg = config.services.moxnotify;

  toLua =
    value:
    let
      recurse = v: toLua v;
      generators = {
        bool = b: if b then "true" else "false";
        int = toString;
        float = toString;
        string = s: ''"${lib.escape [ ''"'' ] s}"'';
        path = p: ''"${p}"'';
        null = "nil";
        list = vs: "{\n  ${lib.concatMapStringsSep ",\n  " recurse vs}\n}";
        attrs = vs: ''
          {
            ${lib.concatStringsSep ",\n" (
              lib.mapAttrsToList (k: v: "[${generators.string k}] = ${recurse v}") vs
            )}
          }'';
      };
    in
    if builtins.isAttrs value then
      generators.attrs value
    else if builtins.isList value then
      generators.list value
    else
      generators.${builtins.typeOf value} value;

  inherit (lib) types;
in
{
  imports = [ ./stylix.nix ];

  options.services.moxnotify = {
    enable = lib.mkEnableOption "moxnotify";
    package = lib.mkPackageOption pkgs "moxnotify" { };

    webui.enable = lib.mkEnableOption "moxnotify webui";
    client.enable = lib.mkOption {
      type = types.bool;
      default = cfg.enable;
    };
    scheduler.enable = lib.mkOption {
      type = types.bool;
      default = cfg.enable;
    };
    controlPlane.enable = lib.mkOption {
      type = types.bool;
      default = cfg.enable;
    };
    indexer.enable = lib.mkOption {
      type = types.bool;
      default = cfg.enable;
    };
    searcher.enable = lib.mkOption {
      type = types.bool;
      default = cfg.enable;
    };
    collector.enable = lib.mkOption {
      type = types.bool;
      default = cfg.enable;
    };

    redis = {
      address = lib.mkOption {
        default = null;
        type = types.nullOr types.str;
      };
    };

    logLevel = lib.mkOption {
      default = "info";
      type = types.str;
      description = "MOXNOTIFY_LOG value for moxnotify services";
    };

    settings = lib.mkOption {
      type =
        let
          valueType = types.nullOr (
            types.oneOf [
              types.bool
              types.int
              types.float
              types.str
              types.path
              (types.attrsOf valueType)
              (types.listOf valueType)
            ]
          );
        in
        valueType;
      default = { };
      description = ''
        moxnotify configuration in Nix format that will be converted to Lua.
      '';
    };
  };

  config = lib.mkIf cfg.enable {
    xdg.configFile = {
      "mox/moxnotify/config.lua" = lib.mkIf (cfg.settings != { }) {
        text = ''
          -- Generated by Home Manager
          return ${toLua cfg.settings}
        '';
      };
    };

    systemd.user.services = {
      moxnotify-control-plane = lib.mkIf cfg.controlPlane.enable {
        Unit = {
          Description = "Moxnotify Control Plane - gRPC coordination service";

          After = lib.optionals (cfg.redis.address == null) [
            "moxnotify-valkey.service"
          ];

          Requires = lib.optionals (cfg.redis.address == null) [
            "moxnotify-valkey.service"
          ];
        };

        Service = {
          Type = "simple";
          ExecStart = "${cfg.package}/bin/moxnotify-control-plane";
          Restart = "on-failure";
          Environment = "MOXNOTIFY_LOG=${cfg.logLevel}";
        };
      };

      moxnotify-collector = lib.mkIf cfg.collector.enable {
        Unit = {
          Description = "Moxnotify Collector";
          After = lib.optionals cfg.controlPlane.enable [
            "moxnotify-control-plane.service"
          ];
          Requires = lib.optionals cfg.controlPlane.enable [
            "moxnotify-control-plane.service"
          ];
          Wants =
            lib.optionals cfg.scheduler.enable [
              "moxnotify-scheduler.service"
            ]
            ++ lib.optionals cfg.indexer.enable [
              "moxnotify-indexer.service"
            ]
            ++ lib.optionals cfg.client.enable [
              "moxnotify-client.service"
            ];
        };

        Service = {
          Type = "dbus";
          BusName = "org.freedesktop.Notifications";
          ExecStart = "${cfg.package}/bin/moxnotify-collector";
          Restart = "on-failure";
          Environment = "MOXNOTIFY_LOG=${cfg.logLevel}";
        };

        Install.WantedBy = [ "graphical-session.target" ];
      };

      moxnotify-scheduler = lib.mkIf cfg.scheduler.enable {
        Unit = {
          Description = "Moxnotify Scheduler - Notification scheduling service";
          After =
            lib.optionals cfg.collector.enable [
              "moxnotify-collector.service"
            ]
            ++ lib.optionals (cfg.redis.address == null) [
              "moxnotify-valkey.service"
            ];
          Requires = lib.optionals (cfg.redis.address == null) [
            "moxnotify-valkey.service"
          ];
        };

        Service = {
          Type = "simple";
          ExecStart = "${cfg.package}/bin/moxnotify-scheduler";
          Restart = "on-failure";
          Environment = "MOXNOTIFY_LOG=${cfg.logLevel}";
        };
      };

      moxnotify-client = lib.mkIf cfg.client.enable {
        Unit = {
          Description = "Moxnotify Client - Wayland notification display client";
          After =
            lib.optionals cfg.collector.enable [
              "moxnotify-collector.service"
            ]
            ++ [ "moxnotify-scheduler.service" ];
          Requires = [ "moxnotify-scheduler.service" ];
          PartOf = [ "graphical-session.target" ];
        };

        Service = {
          Type = "simple";
          ExecStart = "${cfg.package}/bin/moxnotify-client";
          Restart = "on-failure";
          RestartSec = "5s";
          Environment = "MOXNOTIFY_LOG=${cfg.logLevel}";
        };
      };

      moxnotify-indexer = lib.mkIf cfg.indexer.enable {
        Unit = {
          Description = "Moxnotify Indexer - Notification indexing service";
          After =
            lib.optionals cfg.collector.enable [
              "moxnotify-collector.service"
            ]
            ++ lib.optionals (cfg.redis.address == null) [
              "moxnotify-valkey.service"
            ];
          Requires = lib.optionals (cfg.redis.address == null) [
            "moxnotify-valkey.service"
          ];
        };

        Service = {
          Type = "simple";
          ExecStart = "${cfg.package}/bin/moxnotify-indexer";
          Restart = "on-failure";
          Environment = "MOXNOTIFY_LOG=${cfg.logLevel}";
        };
      };

      moxnotify-valkey = lib.mkIf (cfg.redis.address == null) {
        Unit.Description = "Moxnotify Redis";

        Service = {
          Type = "simple";
          ExecStart = "${pkgs.valkey}/bin/valkey-server";
          Restart = "on-failure";
        };
      };

      moxnotify-searcher = lib.mkIf cfg.searcher.enable {
        Unit = {
          Description = "Moxnotify Searcher - Notification search service";
          ConditionPathExists = "${config.xdg.dataHome}/moxnotify";
        };

        Service = {
          Type = "simple";
          ExecStart = "${cfg.package}/bin/moxnotify-searcher";
          Restart = "on-failure";
        };
      };

      moxnotify-webui = lib.mkIf cfg.webui.enable {
        Unit = {
          Description = "Run moxnotify webui";
          Wants = [ "graphical-session.target" ];
          After = [ "graphical-session.target" ];
        };

        Install.WantedBy = [ "default.target" ];

        Service = {
          Type = "simple";
          Restart = "on-failure";

          ExecStart =
            let
              p = lib.makeBinPath [ pkgs.nodejs ];
            in
            "${pkgs.writeShellScriptBin "run-moxnotify-webui" ''
              PATH="$PATH:${p}" ${pkgs.pnpm}/bin/pnpm --dir ${cfg.package}/share/moxnotify/webui start
            ''}/bin/run-moxnotify-webui";
        };
      };

    };

    home.packages = [ cfg.package ];
  };
}
