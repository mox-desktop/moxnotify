{
  config,
  lib,
  pkgs,
  ...
}:
let
  cfg = config.services.moxnotify;

  toLua =
    value:
    let
      recurse = v: toLua v;
      generators = {
        bool = b: if b then "true" else "false";
        int = toString;
        float = toString;
        string = s: ''"${lib.escape [ ''"'' ] s}"'';
        path = p: ''"${p}"'';
        null = "nil";
        list = vs: "{\n  ${lib.concatMapStringsSep ",\n  " recurse vs}\n}";
        attrs = vs: ''
          {
            ${lib.concatStringsSep ",\n" (
              lib.mapAttrsToList (k: v: "[${generators.string k}] = ${recurse v}") vs
            )}
          }'';
      };
    in
    if builtins.isAttrs value then
      generators.attrs value
    else if builtins.isList value then
      generators.list value
    else
      generators.${builtins.typeOf value} value;

  inherit (lib) types;
in
{
  imports = [ ./stylix.nix ];

  options.services.moxnotify = {
    enable = lib.mkEnableOption "moxnotify";
    package = lib.mkPackageOption pkgs "moxnotify" { };

    webui = {
      enable = lib.mkEnableOption "moxnotify webui";
      searcherAddress = lib.mkOption {
        type = types.str;
        default = "http://localhost:64203";
      };
      port = lib.mkOption {
        type = types.int;
        default = 64204;
        description = "Port for the webui server";
      };
      hostname = lib.mkOption {
        type = types.str;
        default = "localhost";
        description = "Hostname/IP for the webui server to bind to";
      };
    };

    client = {
      enable = lib.mkOption {
        type = types.bool;
        default = cfg.enable;
      };
      settings = lib.mkOption {
        type = lib.types.attrs;
        default = { };
        description = "Configuration for client";
      };
    };
    scheduler = {
      enable = lib.mkOption {
        type = types.bool;
        default = cfg.enable;
      };
      settings = lib.mkOption {
        type = lib.types.attrs;
        default = { };
        description = "Configuration for scheduler";
      };
    };
    controlPlane = {
      enable = lib.mkOption {
        type = types.bool;
        default = cfg.enable;
      };
      settings = lib.mkOption {
        type = lib.types.attrs;
        default = { };
        description = "Configuration for control plane";
      };
    };
    indexer = {
      enable = lib.mkOption {
        type = types.bool;
        default = cfg.enable;
      };
      settings = lib.mkOption {
        type = lib.types.attrs;
        default = { };
        description = "Configuration for indexer";
      };
    };
    searcher = {
      enable = lib.mkOption {
        type = types.bool;
        default = cfg.enable;
      };
      settings = lib.mkOption {
        type = lib.types.attrs;
        default = { };
        description = "Configuration for searcher";
      };
    };
    janitor = {
      enable = lib.mkOption {
        type = types.bool;
        default = cfg.enable;
      };
      settings = lib.mkOption {
        type = lib.types.attrs;
        default = { };
        description = "Configuration for janitor";
      };
    };
    collector = {
      enable = lib.mkOption {
        type = types.bool;
        default = cfg.enable;
      };
      settings = lib.mkOption {
        type = lib.types.attrs;
        default = { };
        description = "Configuration for collector";
      };
    };

    valkey.enable = lib.mkOption {
      type = types.bool;
      default = cfg.enable;
    };

    redis.settings = lib.mkOption {
      type = lib.types.attrs;
      default = { };
      description = "Configuration for collector";
    };

    settings = lib.mkOption {
      type =
        let
          valueType = types.nullOr (
            types.oneOf [
              types.bool
              types.int
              types.float
              types.str
              types.path
              (types.attrsOf valueType)
              (types.listOf valueType)
            ]
          );
        in
        valueType;
      default = { };
      description = ''
        moxnotify configuration in Nix format that will be converted to Lua.
      '';
    };
  };

  config = lib.mkIf cfg.enable {
    xdg.configFile = {
      "mox/moxnotify/config.lua".text = ''
        -- Generated by Home Manager
        return ${toLua cfg.client.settings}
      '';
      "mox/moxnotify/default.nix".text = lib.generators.toPretty { } {
        redis = cfg.redis.settings;
        collector = cfg.collector.settings;
        controlPlane = cfg.controlPlane.settings;
        indexer = cfg.indexer.settings;
        searcher = cfg.searcher.settings;
        scheduler = cfg.scheduler.settings;
        janitor = cfg.janitor.settings;
      };
    };

    xdg.dataFile = lib.mkIf cfg.collector.enable {
      "dbus-1/services/pl.mox.notify.service".text = ''
        [D-BUS Service]
        Name=org.freedesktop.Notifications
        Exec=${cfg.package}/bin/moxnotify-collector
        SystemdService=moxnotify-collector.service
      '';
    };

    systemd.user.services = {
      moxnotify-control-plane = lib.mkIf cfg.controlPlane.enable {
        Unit = {
          Description = "Moxnotify Control Plane - gRPC coordination service";

          After = lib.optionals cfg.valkey.enable [
            "moxnotify-valkey.service"
          ];

          Requires = lib.optionals cfg.valkey.enable [
            "moxnotify-valkey.service"
          ];
          RefuseManualStart = false;
        };

        Service = {
          Type = "simple";
          ExecStart = "${cfg.package}/bin/moxnotify-control-plane";
          Restart = "on-failure";
        };
      };

      moxnotify-collector = lib.mkIf cfg.collector.enable {
        Unit = {
          Description = "Moxnotify Collector";
          After = lib.optionals cfg.controlPlane.enable [
            "moxnotify-control-plane.service"
          ];
          Requires = lib.optionals cfg.controlPlane.enable [
            "moxnotify-control-plane.service"
          ];
          Wants =
            lib.optionals cfg.scheduler.enable [
              "moxnotify-scheduler.service"
            ]
            ++ lib.optionals cfg.indexer.enable [
              "moxnotify-indexer.service"
            ]
            ++ lib.optionals cfg.client.enable [
              "moxnotify-client.service"
            ];
        };

        Service = {
          Type = "dbus";
          BusName = "org.freedesktop.Notifications";
          ExecStart = "${cfg.package}/bin/moxnotify-collector";
          Restart = "on-failure";
        };
      };

      moxnotify-scheduler = lib.mkIf cfg.scheduler.enable {
        Unit = {
          Description = "Moxnotify Scheduler - Notification scheduling service";
          After =
            lib.optionals cfg.collector.enable [
              "moxnotify-collector.service"
            ]
            ++ lib.optionals cfg.valkey.enable [
              "moxnotify-valkey.service"
            ];
          Requires = lib.optionals cfg.valkey.enable [
            "moxnotify-valkey.service"
          ];
          RefuseManualStart = false;
        };

        Service = {
          Type = "simple";
          ExecStart = "${cfg.package}/bin/moxnotify-scheduler";
          Restart = "on-failure";
        };
      };

      moxnotify-client = lib.mkIf cfg.client.enable {
        Unit = {
          Description = "Moxnotify Client - Wayland notification display client";
          After =
            lib.optionals cfg.collector.enable [
              "moxnotify-collector.service"
            ]
            ++ [ "moxnotify-scheduler.service" ];
          Requires = [ "moxnotify-scheduler.service" ];
          RefuseManualStart = false;
        };

        Service = {
          Type = "simple";
          ExecStart = "${cfg.package}/bin/moxnotify-client";
          Restart = "on-failure";
          RestartSec = "5s";
        };
      };

      moxnotify-indexer = lib.mkIf cfg.indexer.enable {
        Unit = {
          Description = "Moxnotify Indexer - Notification indexing service";
          After =
            lib.optionals cfg.collector.enable [
              "moxnotify-collector.service"
            ]
            ++ lib.optionals cfg.valkey.enable [
              "moxnotify-valkey.service"
            ];
          Requires = lib.optionals cfg.valkey.enable [
            "moxnotify-valkey.service"
          ];
          RefuseManualStart = false;
        };

        Service = {
          Type = "simple";
          ExecStart = "${cfg.package}/bin/moxnotify-indexer";
          Restart = "on-failure";
        };
      };

      moxnotify-valkey = lib.mkIf cfg.valkey.enable {
        Unit = {
          Description = "Moxnotify Redis";
          RefuseManualStart = false;
        };

        Service = {
          Type = "simple";
          ExecStart = "${pkgs.valkey}/bin/valkey-server";
          Restart = "on-failure";
        };
      };

      moxnotify-searcher = lib.mkIf cfg.searcher.enable {
        Unit = {
          Description = "Moxnotify Searcher - Notification search service";
          ConditionPathExists = "${config.xdg.dataHome}/moxnotify";
          RefuseManualStart = false;
          PartOf = [ ];
          After = [ ];
        };

        Service = {
          Type = "simple";
          ExecStart = "${cfg.package}/bin/moxnotify-searcher";
          Restart = "on-failure";
        };

        Install = {
          WantedBy = [ "default.target" ];
        };
      };

      moxnotify-janitor = lib.mkIf cfg.janitor.enable {
        Unit = {
          Description = "Moxnotify Janitor - Notification cleanup service";
          ConditionPathExists = "${config.xdg.dataHome}/moxnotify";
          RefuseManualStart = false;
          PartOf = [ ];
          After = [ ];
        };

        Service = {
          Type = "simple";
          ExecStart = "${cfg.package}/bin/moxnotify-janitor";
          Restart = "on-failure";
        };

        Install = {
          WantedBy = [ "default.target" ];
        };
      };

      moxnotify-webui = lib.mkIf cfg.webui.enable {
        Unit = {
          Description = "Run moxnotify webui";
          After = [ "graphical-session.target" ];
          RefuseManualStart = false;
          PartOf = [ ];
        };

        Service = {
          Type = "simple";
          Restart = "on-failure";
          Environment = [ "MOXNOTIFY_SEARCHER_ADDRESS=${cfg.webui.searcherAddress}" ];

          ExecStart =
            let
              p = lib.makeBinPath [ pkgs.nodejs ];
            in
            "${pkgs.writeShellScriptBin "run-moxnotify-webui" ''
              PATH="$PATH:${p}"
              ${pkgs.pnpm}/bin/pnpm --dir ${cfg.package}/share/moxnotify/webui start -H "${toString cfg.webui.hostname}" -p "${toString cfg.webui.port}"
            ''}/bin/run-moxnotify-webui";
        };

        Install = {
          WantedBy = [ "default.target" ];
        };
      };

    };

    home.packages = [ cfg.package ];
  };
}
